%*********************************
%* BFO-FOL Axioms                *
%*********************************

all x (Continuant(x) -> Entity(x)).

all x (IndependentContinuant(x) -> Continuant(x)).

all x (MaterialEntity(x) -> IndependentContinuant(x)).

all x (GenericallyDependentContinuant(x) -> Continuant(x)).

all x (SpecificallyDependentContinuant(x) -> Continuant(x)).

all x (Occurrent(x) -> Entity(x)).

all x (Process(x) -> Occurrent(x)).

all x (TemporalRegion(x) -> Occurrent(x)).

all x (Entity(x) -> (Continuant(x) | Occurrent(x))).

-(exists x (Continuant(x) & Occurrent(x))).

all x (IndependentContinuant(x) -> (-GenericallyDependentContinuant(x) & -SpecificallyDependentContinuant(x))).

all x (GenericallyDependentContinuant(x) -> (-IndependentContinuant(x) & -SpecificallyDependentContinuant(x))).

all x (SpecificallyDependentContinuant(x) -> (-GenericallyDependentContinuant(x) & -IndependentContinuant(x))).

all x (Process(x) -> -TemporalRegion(x)).

all x (TemporalRegion(x) -> -Process(x)).

all x all y all t(continuantOverlapAt(x,y,t) <-> (exists z(continuantPartOfAt(z,x,t) & continuantPartOfAt(z,y,t)))).
 
all x all y all t(properContinuantPartOfAt(x,y,t) <-> (continuantPartOfAt(x,y,t) & -(x=y))).

all x all y all t(continuantPartOfAt(x,y,t) -> (Continuant(x) & Continuant(y) & TemporalRegion(t))).

all x all t ((Continuant(x) & existsAt(x,t))-> continuantPartOfAt(x,x,t)).  

all x all y all t((continuantPartOfAt(x,y,t) & continuantPartOfAt(y,x,t)) -> x=y).

all x all y all z all t ((continuantPartOfAt(x,y,t) & continuantPartOfAt(y,z,t))->continuantPartOfAt(x,z,t)).

all x all y all t(properContinuantPartOfAt(x,y,t) -> (exists z (all w(continuantPartOfAt(w,z,t) <-> (continuantPartOfAt(w,x,t) & continuantPartOfAt(w,y,t)))))).

all x all y all t(continuantOverlapAt(x,y,t) -> (exists z (continuantOverlapAt(z,x,t) & continuantOverlapAt(z,y,t) & all w((continuantOverlapAt(w,x,t) & continuantOverlapAt(w,y,t))-> w=z)))). 
 
all x all y (occurrentOverlap(x,y) <-> (exists z(occurrentPartOf(z,x) & occurrentPartOf(z,y)))). 
 
all x all y (properOccurrentPartOf(x,y) <-> (occurrentPartOf(x,y) & -(x=y))).

all x all y(occurrentPartOf(x,y) -> (Occurrent(x) & Occurrent(y))).

all x all t((TemporalRegion(t) & existsAt(x,t) & Occurrent(x))->occurrentPartOf(x,x)).
 
all x all y ((occurrentPartOf(x,y) & occurrentPartOf(y,x)) -> x=y). 

all x all y all z  ((occurrentPartOf(x,y) & occurrentPartOf(y,z))->occurrentPartOf(x,z)). 

all x all y (properOccurrentPartOf(x,y)-> (exists z(properOccurrentPartOf(z,y) & -(occurrentOverlap(z,x))))).

all x all y (occurrentOverlap(x,y) -> (exists z (all w(occurrentPartOf(w,z) <-> (occurrentPartOf(w,x) & occurrentPartOf(w,y)))))).

all x all t (existsAt(x,t) -> (Entity(x) & TemporalRegion(t) & (Entity(x) <-> TemporalRegion(t)))).

all x all t (existsAt(x,t) -> TemporalRegion(t)). 

all x all y((exists t (TemporalRegion(t) & GenericallyDependentContinuant(x) & genericallyDependsOnAt(x,y,t)))-> all t_1 ((TemporalRegion(t_1) & existsAt(x,t_1))->exists z(genericallyDependsOnAt(x,z,t_1)))).

all x (GenericallyDependentContinuant(x)<->(Continuant(x) & exists y exists t (IndependentContinuant(y) & TemporalRegion(t) & genericallyDependsOnAt(x,y,t)))).

all x all y all t ((TemporalRegion(t) & genericallyDependsOnAt(x,y,t))-> exists z (concretizesAt(z,x,t) & specificallyDependsOnAt(z,y,t))).

all x (Process(x)<->(Occurrent(x) & exists y (properOccurrentPartOf(y,x)))). 

all x (Process(x) -> exists z exists t(TemporalRegion(t) & MaterialEntity(z) & specificallyDependsOnAt(x,z,t))).

%*************************************
%     ARGO AXIOMS                    *
%*************************************

%*********************************
%    CLASS HIERARCHY AXIOMS      *
%*********************************

all x (InformationContentEntity(x) -> GenericallyDependentContinuant(x)). 

all x (CollectionOfStatements(x) -> InformationContentEntity(x)).

all x(Argument(x) -> CollectionOfStatements(x)).

all x(ComplexArgument(x) -> Argument(x)). 

all x(Statement(x) -> InformationContentEntity(x)).

-(exists x(Statement(x) & CollectionOfStatements(x))).

all x(Premise(x) -> (Statement(x) & -(Conclusion(x) | Supposition(x) | Subconclusion(x)))).

all x(Conclusion(x) -> (Statement(x) & -(Premise(x) | Supposition(x) | Subconclusion(x)))).

all x(Supposition(x) -> (Statement(x) & -(Conclusion(x) | Premise(x) | Subconclusion(x)))).

all x(Subconclusion(x) -> (Statement(x) & -(Conclusion(x) | Supposition(x) | Premise(x)))).

all x (LanguageAct(x) -> Process(x)).

all x (ActOfArguing(x) -> (LanguageAct(x) & -(ActOfArgumentCreation(x) | ActOfSupposing(x) | ActOfAffirming(x) | ActOfInferring(x)))).

all x(ActOfArgumentCreation(x) -> (LanguageAct(x) & -(ActOfArguing(x) | ActOfSupposing(x) | ActOfAffirming(x) | ActOfInferring(x)))).

all x(ActOfSupposing(x) -> (LanguageAct(x) & -(ActOfArgumentCreation(x) | ActOfArguing(x) | ActOfAffirming(x) | ActOfInferring(x)))).

all x(ActOfInferring(x) -> (LanguageAct(x) & -(ActOfArgumentCreation(x) | ActOfSupposing(x) | ActOfAffirming(x) | ActOfArguing(x)))).

all x(ActOfAffirming(x) -> (LanguageAct(x) & -(ActOfArgumentCreation(x) | ActOfSupposing(x) | ActOfInferring(x) | ActOfArguing(x)))).

%*********************************
%     A-BOX                      *
%*********************************

Premise(a). 
Conclusion(b). 
Argument(c). 
ActOfArguing(d). 
ActOfInferring(e).
ActOfArgumentCreation(f).
Supposition(g).
Subconclusion(h).
ComplexArgument(i).
ActOfAffirming(j).
ActOfSupposing(k). 

%*********************************
%  CLASS FORMAL PROPERTY AXIOMS  *
%********************************* 

all x(Argument(x) -> (exists y exists z exists t(TemporalRegion(t) & Premise(y) & Conclusion(z) & hasArgumentContextAt(y,x,t) & hasArgumentContextAt(z,x,t)))).

all x(ComplexArgument(x) <-> (exists y exists z exists t(TemporalRegion(t) & -(z=y) & Argument(y) & Argument(z) & continuantPartOfAt(y,x,t) & continuantPartOfAt(z,x,t)))).

all x(Premise(x)-> exists y exists t(TemporalRegion(t) & ActOfAffirming(y) & outputOf(x,y,t))).

all x(Premise(x)-> exists y exists t(TemporalRegion(t) & ActOfInferring(y) & inputOf(x,y,t))).

all x(Conclusion(x)-> exists y exists t(TemporalRegion(t) & (ActOfSupposing(y) | ActOfAffirming(y)) & outputOf(x,y,t))).

all x(Conclusion(x)-> exists y exists t(TemporalRegion(t) & ActOfInferring(y) & outputOf(x,y,t))).

all x(Supposition(x)-> exists y exists t(TemporalRegion(t) & ActOfSupposing(y) & outputOf(x,y,t))).

all x(Supposition(x)-> exists y exists t(TemporalRegion(t) & ActOfInferring(y) & inputOf(x,y,t))).

all x(ActOfArgumentCreation(x) -> exists y exists t(TemporalRegion(t) & Argument(y) & isCreatedBy(y,x,t))). 

all x(ActOfSupposing(x) -> exists y exists z(ActOfInferring(y) & ActOfArgumentCreation(z) & occurrentPartOf(x,z) & occurrentPartOf(y,z))). 

all x(ActOfInferring(x) -> exists y exists z exists t(TemporalRegion(t) & Statement(y) & Statement(z) & inputOf(y,x,t) & outputOf(z,x,t))). 

all x(ActOfInferring(x) -> exists y (ActOfArgumentCreation(y) & occurrentPartOf(x,y))). 

%*********************************
%  RELATION DOMAIN/RANGE AXIOMS  *
%********************************* 

all x all y all t(inputOf(x,y,t) -> (Continuant(x) & Process(y) & TemporalRegion(t))).

all x all y all t(outputOf(x,y,t) -> (Continuant(x) & Process(y) & TemporalRegion(t))).

all x all y all t(hasArgumentContextAt(x,y,t) -> ((Premise(x) | Conclusion(x) | Supposition(x) | Subconclusion(x)) & Argument(y) & TemporalRegion(t))).

all x all y all t(isCreatedBy(x,y,t) -> (Argument(x) & ActOfArgumentCreation(y) & TemporalRegion(t))). 

%**********************************
% RELATION FORMAL PROPERTY AXIOMS *
%********************************** 

all x all y all z all t((Premise(x) & hasArgumentContextAt(x,y,t) & hasArgumentContextAt(x,z,t))-> y=z).

all x all y all z all t((Conclusion(x) & hasArgumentContextAt(x,y,t) & hasArgumentContextAt(x,z,t))-> y=z).

all x all y all z all t((Supposition(x) & hasArgumentContextAt(x,y,t) & hasArgumentContextAt(x,z,t))-> y=z).

all x all y all z all t((isCreatedBy(x,y,t) & isCreatedBy(z,y,t))-> x=z).

all x all y all z all t((isCreatedBy(x,y,t) & isCreatedBy(x,z,t))-> y=z).

all x all y all z all t ((Premise(x) & hasArgumentContextAt(x,y,t) & ActOfInferring(z) & inputOf(x,z,t))-> exists w(ActOfArgumentCreation(w) & occurrentPartOf(z,w) & isCreatedBy(y,w,t))). 

%**********************************
%      IN PROGESS                 * 
%********************************** 

%all x all y all z all w all t ((Premise(x) & hasArgumentContextAt(x,y,t) & ActOfInferring(z) & inputOf(x,z,t) & ActOfArgumentCreation(w) & occurrentPartOf(z,w))-> isCreatedBy(y,w,t)).

%all x(Subconclusion(x) -> (exists y exists z exists p1 exists p2 exists p3 exists a1 exists a2 exists a3 exists t(ActOfInferring(y) & ActOfInferring(z) & inputOf(x,y,t) & inputOf(x,z,t) & ActOfArgumentCreation(p1) & ActOfArgumentCreation(p2) & occurrentPartOf(y,p1) & occurrentPartOf(z,p2) & Argument(a1) & Argument(a2) & isCreatedBy(a1,p1,t) & isCreatedBy(a2,p2,t) & hasArgumentContextAt(x,a1,t) & hasArgumentContextAt(x,a2,t) & ActOfArgumentCreation(p3) & Argument(a3) & isCreatedBy(a3,p3,t) &
occurrentPartOf(p1,p3) & occurrentPartOf(p2,p3) & continuantPartOfAt(a1,a3,t) & continuantPartOfAt(a2,a3,t) & hasArgumentContextAt(x,a3,t)))). 

%**********************************
%      Theorems/Models            *
%********************************** 

%all x all t(-hasArgumentContextAt(x,x,t)). 
%IRREFLEXIVE (PROVED)

%all x all y all t(hasArgumentContextAt(x,y,t) -> -hasArgumentContextAt(y,x,t)). 
%ASYMMETRIC (PROVED)
